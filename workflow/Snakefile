import os
from snakemake.utils import min_version
min_version("9.3.3")

# Config
configfile: "config/config.yaml"

if not config.get("threads"):
    config["threads"] = max(4, os.cpu_count() // 4)
    
include: "rules/detect_samples.smk"
include: "rules/fastp.smk"
include: "rules/bowtie2.smk"
include: "rules/bam_to_bigwig.smk"
include: "rules/macs3.smk"

SAMPLE_LIST = get_sample_list(config)
SAMPLES = get_sample_type_map(config["raw_dir"], samples=SAMPLE_LIST)
print("\n[SAMPLE TYPE DETECTION]")
for sample, s_type in SAMPLES.items():
    print(f"  - {sample}: {s_type}")

PE_SAMPLES = [s for s, t in SAMPLES.items() if t == "PE"]
SE_SAMPLES = [s for s, t in SAMPLES.items() if t == "SE"]

all_inputs = []
# Fastp outputs
all_inputs += expand("results/fastp/{sample}_pe_fastp.html", sample=PE_SAMPLES)
all_inputs += expand("results/fastp/{sample}_se_fastp.html", sample=SE_SAMPLES)
# Bowtie2 BAMs
all_inputs += expand("results/bowtie2/{sample}/{sample}_pe.bam", sample=PE_SAMPLES)
all_inputs += expand("results/bowtie2/{sample}/{sample}_se.bam", sample=SE_SAMPLES)
if config.get("filter_blacklist", True):
    all_inputs += expand("results/bowtie2/{sample}/{sample}.filtered.bam", sample=PE_SAMPLES)
    all_inputs += expand("results/bigwig/{sample}.filtered.bw", sample=PE_SAMPLES + SE_SAMPLES)
    all_inputs += expand("results/macs3/{sample}/{sample}.filtered_peaks.narrowPeak", sample=PE_SAMPLES + SE_SAMPLES)
    all_inputs += expand("results/macs3/{sample}/{sample}.filtered_summits.bed", sample=PE_SAMPLES + SE_SAMPLES)
all_inputs += expand("results/bigwig/{sample}.bw", sample=PE_SAMPLES + SE_SAMPLES)
all_inputs += expand("results/macs3/{sample}/{sample}_peaks.xls", sample=PE_SAMPLES + SE_SAMPLES)

rule all:
    input: all_inputs
