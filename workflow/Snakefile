import os
from snakemake.utils import min_version
min_version("9.3.3")

configfile: "config.yaml"

include: "rules/common.smk"
include: "rules/qc_fastp.smk"
include: "rules/align_bowtie2.smk"
include: "rules/bam_to_bigwig.smk"
include: "rules/macs3.smk"

MULTIQC_TARGET = (
    f"{OUTDIR}/qc/multiqc/multiqc_report.html"
    if config.get("generate_multiqc", True)
    else None
)

PRIMARY_TARGETS = []
PRIMARY_TARGETS += expand(f"{OUTDIR}/qc/fastp/{{sample}}/merged_fastp_final.html", sample=SAMPLES)
PRIMARY_TARGETS += expand(f"{OUTDIR}/qc/fastp/{{sample}}/merged_fastp_final.json", sample=SAMPLES)
if MULTIQC_TARGET:
    PRIMARY_TARGETS.append(MULTIQC_TARGET)
if KEEP_BAM:
    PRIMARY_TARGETS += [final_bam_path(sample) for sample in SAMPLES]
    PRIMARY_TARGETS += [final_bai_path(sample) for sample in SAMPLES]
PRIMARY_TARGETS += expand(f"{OUTDIR}/bigwig/{{sample}}/{{sample}}.raw.bw", sample=SAMPLES)
PRIMARY_TARGETS += expand(f"{OUTDIR}/bigwig/{{sample}}/{{sample}}.normalized.bw", sample=SAMPLES)
if config.get("run_macs3", True):
    PRIMARY_TARGETS += expand(f"{OUTDIR}/macs3/{{sample}}/{{sample}}_peaks.narrowPeak", sample=SAMPLES)
    PRIMARY_TARGETS += expand(f"{OUTDIR}/macs3/{{sample}}/{{sample}}_summits.bed", sample=SAMPLES)
    PRIMARY_TARGETS += expand(f"{OUTDIR}/macs3/{{sample}}/{{sample}}_peaks.xls", sample=SAMPLES)

CLEANUP_PICARD_TARGET = (
    f"{OUTDIR}/qc/tmp_cleanup/picard_tmp_cleanup.done"
    if config.get("cleanup_picard_tmp", True)
    else None
)

ALL_TARGETS = list(PRIMARY_TARGETS)
if CLEANUP_PICARD_TARGET:
    ALL_TARGETS.append(CLEANUP_PICARD_TARGET)

rule all:
    input:
        ALL_TARGETS


if config.get("cleanup_picard_tmp", True):
    rule cleanup_picard_tmp:
        input:
            PRIMARY_TARGETS
        output:
            CLEANUP_PICARD_TARGET
        shell:
            r"""
            set -euo pipefail
            rm -rf "{PICARD_TMPDIR}"
            mkdir -p $(dirname {output})
            touch {output}
            """
